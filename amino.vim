" amino.vim colour scheme by Stochastica (vel Horosphere)
" Supports 16-coloured, 256-coloured, and GUI.

" Cleanup
set background=dark
highlight clear
if exists("syntax_on")
	syntax reset
endif

" Global variable for colour name, the rest are all local
let g:colors_name = "amino"

" If encoding is UTF-8, use special characters for space and newline
if &encoding == "utf-8"
	set listchars=eol:¬,tab:➝\ ,trail:·,space:␣
	set list
endif

" g = gui, t = terminal
let s:cg_foreground='#C8C8C8' " Light grey, not fully white
let s:cg_background='#1E1E1E' " Dark grey, not fully black
let s:cg_lineNum='#898D8E' " Grey
let s:cg_activeLineNum='#FFEE62' " Yellow
let s:cg_activeLineBG='#2E2E2E' " Disabled
let s:cg_nonText='#585858' " Grey, used when tabs are shown explicitly
let s:cg_pMenuBG='#5F5F87'
let s:cg_pMenuFG=s:cg_foreground
let s:cg_pMenuSelected='#FF5F00' " Orange
let s:cg_matchParen='#FFEE62' " Yellow

let s:cg_comment='#93C763' " Green/Lime
let s:cg_constant='#ECBD3D' " Yellow
let s:cg_identifier='#A8A8A8' " Lighter grey than preprocessor
let s:cg_preprocessor='#808080' " Grey
let s:cg_preprocMacro='#BD63C5' " Magenta
let s:cg_variable='#D28408' " Orange
let s:cg_method='#DFDFFF'
let s:cg_special='#56B6C2' " Cyan
let s:cg_statement='#569CD6' " Blue
let s:cg_label='#FEFE55' " Yellow
let s:cg_string='#55BE67' " Mild green
let s:cg_type='#55FE55' " Green
let s:cg_enumConst=s:cg_preprocMacro
let s:cg_syntaxError='#AF0000' " Dark red
let s:cg_syntaxWarning='#AF8700' " Yellow

"let s:cg_annotation='#1AB166' " Orange
let s:cg_annotation='#E1AE48' " Orange

" Tex
let s:cg_tex=s:cg_foreground
let s:cg_tex_section=s:cg_preprocMacro
let s:cg_tex_statement=s:cg_statement
let s:cg_tex_beginEndName=s:cg_constant
let s:cg_vim_option=s:cg_preprocMacro

" Plugin based
let s:cg_cANSI='#AA63A4' " Magenta

" The following are generated by a script
if &t_Co == 256 " 256 colour xterm
	let s:ct_foreground='251'
	let s:ct_background='234'
	let s:ct_lineNum='245'
	let s:ct_activeLineNum='227'
	let s:ct_activeLineBG='236'
	let s:ct_nonText='240'
	let s:ct_pMenuBG='60'
	let s:ct_pMenuFG=s:ct_foreground
	let s:ct_pMenuSelected='202'
	let s:ct_matchParen='245'

	let s:ct_comment='113'
	let s:ct_constant='215'
	let s:ct_identifier='248'
	let s:ct_preprocessor='244'
	let s:ct_preprocMacro='134'
	let s:ct_variable='172'
	let s:ct_method='189'
	let s:ct_special='73'
	let s:ct_statement='74'
	let s:ct_label='227'
	let s:ct_string='71'
	let s:ct_type='83'
	let s:ct_enumConst=s:ct_preprocMacro
	let s:ct_syntaxError='124'
	let s:ct_syntaxWarning='136'

	let s:ct_annotation='179' "'35'

	let s:ct_tex=s:ct_foreground
	let s:ct_tex_section=s:ct_preprocMacro
	let s:ct_tex_statement=s:ct_statement
	let s:ct_tex_beginEndName=s:ct_constant

	let s:ct_vim_option=s:ct_preprocMacro

else " 16 colour
	let s:ct_foreground='White'
	let s:ct_background='Black'
	let s:ct_lineNum='DarkGrey'
	let s:ct_activeLineNum='Yellow'
	let s:ct_activeLineBG='Black'
	let s:ct_nonText='DarkGrey'
	let s:ct_pMenuBG='DarkGrey'
	let s:ct_pMenuFG=s:ct_foreground
	let s:ct_pMenuSelected='Yellow'
	let s:ct_matchParen='Yellow'

	let s:ct_comment='Green'
	let s:ct_constant='Yellow'
	let s:ct_identifier='Grey'
	let s:ct_preprocessor='DarkGrey'
	let s:ct_preprocMacro='Magenta'
	let s:ct_variable='DarkYellow'
	let s:ct_method='White'
	let s:ct_special='Cyan'
	let s:ct_statement='Blue'
	let s:ct_label='Yellow'
	let s:ct_string='DarkGreen'
	let s:ct_type='Blue'
	let s:ct_enumConst=s:ct_preprocMacro
	let s:ct_syntaxError='DarkRed'
	let s:ct_syntaxWarning='DarkYellow'

	let s:ct_annotation='DarkYellow'

	let s:ct_tex=s:ct_foreground
	let s:ct_tex_section=s:ct_preprocMacro
	let s:ct_tex_statement=s:ct_statement
	let s:ct_tex_beginEndName=s:ct_constant

	let s:ct_vim_option=s:ct_preprocMacro

	let s:ct_cANSI='Magenta'
endif

" Base
if has('gui_running') " Enables background
	exe 'hi Normal cterm=none gui=none ctermfg='.s:ct_foreground
		\.' guifg='.s:cg_foreground
		\.' ctermbg='.s:ct_background
		\.' guibg='.s:cg_background
else " Disable background in terminal mode
	exe 'hi Normal term=none cterm=none gui=none ctermfg='.s:ct_foreground
		\.' guifg='.s:cg_foreground
		\.' ctermbg=none'
endif
exe 'hi LineNr ctermfg='.s:ct_lineNum.' guifg='.s:cg_lineNum
exe 'hi CursorLineNR term=bold cterm=bold gui=bold ctermfg='.s:ct_activeLineNum
	\.' guifg='.s:cg_activeLineNum
hi clear CursorLine
hi CursorLine term=underline cterm=underline gui=underline
"exe 'hi CursorLine ctermbg='.s:ct_activeLineBG.' guibg='.s:cg_activeLineBG
exe 'hi SpecialKey ctermfg='.s:ct_nonText.' guifg='.s:cg_nonText
exe 'hi NonText ctermfg='.s:ct_nonText.' guifg='.s:cg_nonText
exe 'hi pMenu ctermfg='.s:ct_pMenuFG.' ctermbg='.s:ct_pMenuBG
	\.' guifg='.s:cg_pMenuFG.' guibg='.s:cg_pMenuBG
exe 'hi pMenuSel ctermfg='.s:ct_pMenuSelected.' ctermbg='.s:ct_pMenuBG
	\.' guifg='.s:cg_pMenuSelected.' guibg='.s:cg_pMenuBG
hi clear MatchParen
exe 'hi MatchParen term=bold cterm=bold gui=bold ctermfg='.s:ct_matchParen
	\.' guifg='.s:cg_matchParen

" Highlighting
exe 'hi Comment ctermfg='.s:ct_comment.' guifg='.s:cg_comment
exe 'hi Constant ctermfg='.s:ct_constant.' guifg='.s:cg_constant
exe 'hi Identifier ctermfg='.s:ct_identifier.' guifg='.s:cg_identifier
exe 'hi PreProc ctermfg='.s:ct_preprocessor.' guifg='.s:cg_preprocessor
exe 'hi Macro ctermfg='.s:ct_preprocMacro.' guifg='.s:cg_preprocMacro
exe 'hi Variable cterm=bold gui=bold ctermfg='.s:ct_variable
	\.' guifg='.s:cg_variable
exe 'hi Member cterm=none gui=none ctermfg='.s:ct_foreground
	\.' guifg='.s:cg_foreground
exe 'hi FieldDecl cterm=italic gui=italic ctermfg='.s:ct_variable
	\.' guifg='.s:cg_variable
exe 'hi Special ctermfg='.s:ct_special.' guifg='.s:cg_special
exe 'hi Statement cterm=none gui=none ctermfg='.s:ct_statement
	\.' guifg='.s:cg_statement
highlight link StorageClass Statement
highlight link Structure Statement
highlight link Typedef Statement
highlight link EnumConstant Constant
exe 'hi Repeat cterm=bold gui=bold ctermfg='.s:ct_statement
	\.' guifg='.s:cg_statement
highlight link cRepeat Repeat
exe 'hi Label ctermfg='.s:ct_label.' guifg='.s:cg_label
exe 'hi String ctermfg='.s:ct_string.' guifg='.s:cg_string
exe 'hi Type cterm=none gui=none ctermfg='.s:ct_type.' guifg='.s:cg_type
exe 'hi Error ctermbg='.s:ct_syntaxError.' guibg='.s:cg_syntaxError

exe 'hi Annotation term=bold cterm=bold gui=bold ctermfg='.s:ct_annotation.' guifg='.s:cg_annotation

" Tex
exe 'hi texMath ctermfg='.s:ct_tex.' guifg='.s:cg_tex
exe 'hi texStatement ctermfg='.s:ct_tex_statement.' guifg='.s:cg_tex_statement
exe 'hi texGreek cterm=italic gui=italic ctermfg='.s:ct_tex_statement.' guifg='.s:cg_tex_statement
highlight link texSuperscript texMath
highlight link texSuperscripts texMath
highlight link texSubscript texMath
highlight link texSubscripts texMath
exe 'hi texSection ctermfg='.s:ct_tex_section.' guifg='.s:cg_tex_section
exe 'hi texBeginEndName ctermfg='.s:ct_tex_beginEndName.' guifg='.s:cg_tex_beginEndName

" Vim
exe 'hi vimOption ctermfg='.s:ct_vim_option.' guifg='.s:cg_vim_option

" Plugins
"
highlight link cppModifier Statement
exe 'hi CXXMethod cterm=italic, gui=italic ctermfg='.s:ct_method
	\.' guifg='.s:cg_method
highlight link Constructor CXXMethod
highlight link Destructor CXXMethod
highlight link ConversionFunction CXXMethod
exe 'hi YcmWarningSign ctermbg='.s:ct_syntaxWarning
	\.' guibg='.s:cg_syntaxWarning

" Airline
if exists('g:airline_powerline_fonts')
	" This is merged from bubblegum
	let s:gui_dark_gray = '#303030'
	let s:cterm_dark_gray = 236
	let s:gui_med_gray_hi = '#444444'
	let s:cterm_med_gray_hi = 238
	let s:gui_med_gray_lo = '#3a3a3a'
	let s:cterm_med_gray_lo = 237
	let s:gui_light_gray = '#b2b2b2'
	let s:cterm_light_gray = 249
	let s:gui_green = '#afd787'
	let s:cterm_green = 150
	let s:gui_blue = '#87afd7'
	let s:cterm_blue = 110
	let s:gui_purple = '#afafd7'
	let s:cterm_purple = 146
	let s:gui_orange = '#d7af5f'
	let s:cterm_orange = 179
	let s:gui_red = '#d78787'
	let s:cterm_red = 174
	let s:gui_pink = '#d7afd7'
	let s:cterm_pink = 182
	
	let g:airline#themes#{g:colors_name}#palette = {}
	
	" Normal mode
	let s:N1 = [s:gui_dark_gray, s:gui_green, s:cterm_dark_gray, s:cterm_green]
	let s:N2 = [s:gui_light_gray, s:gui_med_gray_lo, s:cterm_light_gray, s:cterm_med_gray_lo]
	let s:N3 = [s:gui_green, s:gui_med_gray_hi, s:cterm_green, s:cterm_med_gray_hi]
	let g:airline#themes#{g:colors_name}#palette.normal = airline#themes#generate_color_map(s:N1, s:N2, s:N3)
	let g:airline#themes#{g:colors_name}#palette.normal_modified = {
	      \ 'airline_c': [s:gui_orange, s:gui_med_gray_hi, s:cterm_orange, s:cterm_med_gray_hi, ''],
	      \ }
	
	" Insert mode
	let s:I1 = [s:gui_med_gray_hi, s:gui_blue, s:cterm_med_gray_hi, s:cterm_blue]
	let s:I3 = [s:gui_blue, s:gui_med_gray_hi, s:cterm_blue, s:cterm_med_gray_hi]
	let g:airline#themes#{g:colors_name}#palette.insert = airline#themes#generate_color_map(s:I1, s:N2, s:I3)
	let g:airline#themes#{g:colors_name}#palette.insert_modified = copy(g:airline#themes#{g:colors_name}#palette.normal_modified)
	let g:airline#themes#{g:colors_name}#palette.insert_paste = {
	      \ 'airline_a': [s:gui_dark_gray, s:gui_orange, s:cterm_dark_gray, s:cterm_orange, ''],
	      \ }
	
	" Replace mode
	let g:airline#themes#{g:colors_name}#palette.replace = {
	      \ 'airline_a': [s:gui_dark_gray, s:gui_red, s:cterm_dark_gray, s:cterm_red, ''],
	      \ 'airline_c': [s:gui_red, s:gui_med_gray_hi, s:cterm_red, s:cterm_med_gray_hi, ''],
	      \ }
	let g:airline#themes#{g:colors_name}#palette.replace_modified = copy(g:airline#themes#{g:colors_name}#palette.insert_modified)
	
	" Visual mode
	let s:V1 = [s:gui_dark_gray, s:gui_pink, s:cterm_dark_gray, s:cterm_pink]
	let s:V3 = [s:gui_pink, s:gui_med_gray_hi, s:cterm_pink, s:cterm_med_gray_hi]
	let g:airline#themes#{g:colors_name}#palette.visual = airline#themes#generate_color_map(s:V1, s:N2, s:V3)
	let g:airline#themes#{g:colors_name}#palette.visual_modified = copy(g:airline#themes#{g:colors_name}#palette.insert_modified)
	
	" Inactive window
	let s:IA = [s:gui_light_gray, s:gui_med_gray_hi, s:cterm_light_gray, s:cterm_med_gray_hi, '']
	let g:airline#themes#{g:colors_name}#palette.inactive = airline#themes#generate_color_map(s:IA, s:IA, s:IA)
	let g:airline#themes#{g:colors_name}#palette.inactive_modified = {
	      \ 'airline_c': [s:gui_orange, '', s:cterm_orange, '', ''],
	      \ }
	
	" CtrlP
	if !get(g:, 'loaded_ctrlp', 0)
	  finish
	endif
	let g:airline#themes#{g:colors_name}#palette.ctrlp = airline#extensions#ctrlp#generate_color_map(
	      \ [ s:gui_orange, s:gui_med_gray_hi, s:cterm_orange, s:cterm_med_gray_hi, '' ] ,
	      \ [ s:gui_orange, s:gui_med_gray_lo, s:cterm_orange, s:cterm_med_gray_lo, '' ] ,
	      \ [ s:gui_dark_gray, s:gui_green, s:cterm_dark_gray, s:cterm_green, 'bold' ] )

endif
